project (CheMPS2)
set (CheMPS2_VERSION      "1.5")
set (CheMPS2_AUTHORS      "Sebastian Wouters")
set (CheMPS2_DESCRIPTION  "A spin-adapted implementation of DMRG for ab initio quantum chemistry")
set (CheMPS2_EMAIL        "sebastianwouters@gmail.com")
set (CheMPS2_URL          "https://github.com/SebWouters/CheMPS2")
set (CheMPS2_LICENSE      "GNU General Public License, version 2")
set (CheMPS2_INC_RELPATH  "include/chemps2")
set (CheMPS2_LIB_SOVERSION 1)

include (GNUInstallDirs)

option (MKL                  "Compile using the MKL"                   OFF)
option (BUILD_DOXYGEN        "Use Doxygen to create a HTML/PDF manual" OFF)
option (BUILD_SPHINX         "Build the user manual with Sphinx"       OFF)
option (STATIC_ONLY          "Compile only the static library"         OFF)
option (ENABLE_TESTS         "Compile the tests"                       ON)
option (ENABLE_XHOST         "Enable processor-specific optimizations" ON)

if (MKL)
    cmake_minimum_required (VERSION 2.8.11)
else (MKL)
    cmake_minimum_required (VERSION 2.8)
endif (MKL)

set (CMAKE_VERBOSE_MAKEFILE OFF)

find_package (OpenMP)
if (OPENMP_FOUND)
    set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}")
    set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
endif (OPENMP_FOUND)

include (CheckCXXCompilerFlag)

if (ENABLE_XHOST)
    check_cxx_compiler_flag (-xHost HAS_XHOST)
    check_cxx_compiler_flag (-march=native HAS_MARCH_NATIVE)
    if (HAS_XHOST)
        set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -xHost")
    elseif (HAS_MARCH_NATIVE)
        set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -march=native")
    endif ()
endif (ENABLE_XHOST)

check_cxx_compiler_flag (-ipo HAS_IPO)

if (HAS_IPO)
    set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -ipo")
endif (HAS_IPO)

set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -g -O3")

set (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CheMPS2_SOURCE_DIR}/CMake/")

link_directories ($ENV{LD_LIBRARY_PATH})
if (MKL)
    set ( ENV{BLA_VENDOR} "Intel10_64lp" )
endif (MKL)
find_package (HDF5   REQUIRED)
find_package (LAPACK REQUIRED)
find_package (GSL    REQUIRED)

enable_testing ()
add_subdirectory (CheMPS2)
if (ENABLE_TESTS)
    add_subdirectory (tests)
endif (ENABLE_TESTS)
add_subdirectory (PyCheMPS2)
if (BUILD_SPHINX)
    add_subdirectory (sphinx)
endif (BUILD_SPHINX)

if (BUILD_DOXYGEN)
    find_package (Doxygen)
    if (NOT DOXYGEN_FOUND)
        message (FATAL_ERROR "Doxygen is needed to build the documentation. Please install it correctly.")
    endif (NOT DOXYGEN_FOUND)
    configure_file (${CheMPS2_SOURCE_DIR}/CheMPS2/Doxyfile.in ${CheMPS2_BINARY_DIR}/Doxyfile @ONLY IMMEDIATE)
    add_custom_target (doc COMMAND ${DOXYGEN_EXECUTABLE} ${CheMPS2_BINARY_DIR}/Doxyfile SOURCES ${CheMPS2_BINARY_DIR}/Doxyfile)
endif (BUILD_DOXYGEN)

